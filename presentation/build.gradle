apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion setup.compileSdk
  buildToolsVersion setup.buildTools

  defaultConfig {
    minSdkVersion setup.minSdk
    targetSdkVersion setup.targetSdk

    applicationId setup.androidApplicationId
    versionCode setup.androidVersionCode
    versionName setup.androidVersionName
    testInstrumentationRunner setup.testInstrumentationRunner
    testApplicationId setup.testApplicationId
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
  }

  lintOptions {
    quiet true
    abortOnError false
    ignoreWarnings true
    disable 'InvalidPackage' //Some libraries have issues with this.
    disable 'OldTargetApi' //Lint gives this warning but SDK 20 would be Android L Beta.
    disable 'IconDensities' //For testing purpose. This is safe to remove.
    disable 'IconMissingDensityFolder' //For testing purpose. This is safe to remove.
  }

  signingConfigs {
    debug {
      storeFile file('../buildsystem/debug.keystore')
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
  }
}


dependencies {
  compile project(':domain')
  compile project(':data')

  apt "com.google.dagger:dagger-compiler:${versions.daggerVersion}"
  compile "com.google.dagger:dagger:${versions.daggerVersion}"
  compile "com.jakewharton:butterknife:${versions.butterKnifeVersion}"
  compile "com.android.support:recyclerview-v7:${versions.recyclerViewVersion}"
  compile "io.reactivex.rxjava2:rxjava:${versions.rxJavaVersion}"
  compile "io.reactivex.rxjava2:rxandroid:${versions.rxAndroidVersion}"
  compile "com.jakewharton.timber:timber:${versions.timberVersion}"

  compile "com.google.firebase:firebase-core:${versions.firebaseCoreVersion}"

  provided "javax.annotation:jsr250-api:${versions.javaxAnnotationVersion}"

  androidTestCompile "org.mockito:mockito-core:${versions.mockitoVersion}"
  androidTestCompile "com.google.dexmaker:dexmaker:${versions.dexmakerVersion}"
  androidTestCompile "com.google.dexmaker:dexmaker-mockito:${versions.dexmakerVersion}"
  androidTestCompile "com.android.support.test.espresso:espresso-core:${versions.espressoVersion}"
  androidTestCompile "com.android.support.test:testing-support-lib:${versions.testingSupportLibVersion}"

  //Development
  compile "com.squareup.leakcanary:leakcanary-android:${versions.leakCanaryVersion}"
}

